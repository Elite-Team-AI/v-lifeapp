apply plugin: 'com.android.application'

android {
    namespace = "app.vlife.android"
    compileSdk = rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "app.vlife.android"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern = '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    signingConfigs {
        release {
            // For local builds, create a keystore and set these environment variables:
            // VLIFE_KEYSTORE_PATH, VLIFE_KEYSTORE_PASSWORD, VLIFE_KEY_ALIAS, VLIFE_KEY_PASSWORD
            // Or use the default debug keystore for unsigned builds
            if (System.getenv("VLIFE_KEYSTORE_PATH")) {
                storeFile file(System.getenv("VLIFE_KEYSTORE_PATH"))
                storePassword System.getenv("VLIFE_KEYSTORE_PASSWORD")
                keyAlias System.getenv("VLIFE_KEY_ALIAS")
                keyPassword System.getenv("VLIFE_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // Use release signing config if keystore is configured
            if (System.getenv("VLIFE_KEYSTORE_PATH")) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            debuggable true
        }
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
